// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
        mavenCentral()

        /*
        maven {
            url  "http://dl.bintray.com/countly/maven"
        }
        */
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

def File getVersionPropertiesFile(moduleName) {
    def versionPropsFile = file(moduleName + '/version.properties')

    println("getVersionCode: loading '" + versionPropsFile + "'")

    if (!versionPropsFile.exists()) {
        versionPropsFile.createNewFile()
    }

    return versionPropsFile
}

def Properties readVersionProperties(moduleName) {
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(getVersionPropertiesFile(moduleName)))

    if (!versionProps['VERSION_CODE']) {
        versionProps['VERSION_CODE'] = "1"
    }

    return versionProps
}

def int getVersionCode(moduleName) {
    def Properties versionProps = readVersionProperties(moduleName)

    return Integer.parseInt(versionProps['VERSION_CODE'].toString()).intValue()
}

def int updateVersionCode(moduleName) {
    def Properties versionProps = readVersionProperties(moduleName)
    def versionCode = getVersionCode(moduleName) + 1;

    versionProps['VERSION_CODE'] = versionCode.toString()
    versionProps.store(getVersionPropertiesFile(moduleName).newWriter(), null)

    println("updateVersionCode: " + versionCode)

    return versionCode
}
